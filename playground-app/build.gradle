plugins {
    id 'org.springframework.boot'        version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.node-gradle.node'     version '2.2.3'
    id 'io.freefair.lombok'              version '5.0.0-rc6'
    id 'java'
}

group = 'by.arhor'
version = '0.0.1-SNAPSHOT'

description = 'playground-app'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

node {
    version  = '13.11.0'
    download = true
}

bootRun {
    args = ['--spring.profiles.active=dev']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def jsRootDir = "${project.projectDir}"

task npmAuditFix (type: NpmTask, dependsOn: npmInstall) {
    description = 'Verify and fix vulnerabilities in dependencies'
    workingDir = file(jsRootDir)
    args = ['audit', 'fix']
}

task clientBuild (type: NpmTask, dependsOn: npmAuditFix) {
    description = 'Builds production version of the university standalone app client'
    workingDir = file(jsRootDir)
    args = ['run', 'build']
}

compileJava { dependsOn(clientBuild) }
